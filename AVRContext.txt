define hook-stop
context
end
define context
printf "r00-r05\t%02x\t%02x\t%02x\t%02x\t%02x\t%02x\n", $r0, $r1, $r2, $r3, $r4, $r5
printf "r06-r11\t%02x\t%02x\t%02x\t%02x\t%02x\t%02x\n", $r6, $r7, $r8, $r9, $r10, $r11
printf "r12-r17\t%02x\t%02x\t%02x\t%02x\t%02x\t%02x\n", $r12, $r13, $r14, $r15, $r16, $r17
printf "r18-r23\t%02x\t%02x\t%02x\t%02x\t%02x\t%02x\n", $r18, $r19, $r20, $r21, $r22, $r23
printf "r24-r29\t%02x\t%02x\t%02x\t%02x\t%02x\t%02x\n", $r24, $r25, $r26, $r27, $r28, $r29
printf "r30-r31\t%02x\t%02x\tpc\t%04x\tpc2\t%04x\tsp\t%06x\n", $r30, $r31, $pc, (int)$pc/1, $sp
printf "x %04x\ty %04x\tz %04x\n", *(short*)26, *(short*)28, *(short*)30
printf "SREG %02x %c %c %c %c %c %c %c %c\n", $SREG, 'i'&~((($SREG&0x80)!=0)<<5), 't'&~((($SREG&0x40)!=0)<<5), 'h'&~((($SREG&0x20)!=0)<<5), 's'&~((($SREG&0x10)!=0)<<5), 'v'&~((($SREG&0x08)!=0)<<5), 'n'&~((($SREG&0x04)!=0)<<5), 'z'&~((($SREG&0x02)!=0)<<5), 'c'&~((($SREG&0x01)!=0)<<5)
x/15i ((int)$pc-12)<0?0:$pc-12
bt
end
define bgdb
 b *(void (*)())((int)$arg0)
end
document bgdb
 Break using address from GDB
end
define bida
 b *(void (*)())((int)$arg0*2)
end
document bida
 Break using address from IDA
end
define pgdb
 printf "ROM:%x\n",(int)$arg0>>1
end
document pgdb
 Print IDA address from GDB
end
define pida
 printf "%x\n",(int)$arg0*2
end
document pida
 Print GDB address from IDA
end

